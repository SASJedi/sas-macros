%macro fdelete(fname);
%local rc fileref MsgType;
%let MsgType=NOTE;
%if %superq(fname)= %then %do; 
   %let MsgType=ERROR;
   %put;
   %put &MsgType: &SYSMACRONAME Error:;
   %put &MsgType- You must supply a fully-qualified file name.;
   %put;
   %goto Syntax; 
%end;
%if %SUPERQ(dsn)= ? %then %do;
%Syntax:
   %put;
   %put &MsgType: &SYSMACRONAME documentation:;
   %put &MsgType- Purpose: Delete a file;
   %put &MsgType- Syntax: %nrstr(%%)&SYSMACRONAME(fname);
   %put &MsgType-    fname:  Fully-qualified file name.;
   %put ;
   %put &MsgType- Example: %nrstr(%%)&SYSMACRONAME(/home/student/myfile.txt);;
   %put ;
   %put &MsgType- Use ? to print documentation to the SAS log.;
   %put;
   %return;
%end; 
   %let rc=%sysfunc(filename(fileref,%superq(fname)));
   %if not &rc %then %let rc=%sysfunc(fdelete(&fileref));
   %if &rc %then %do;
      %let msg=%qsysfunc(sysmsg());
      %put &=rc - Could not delete %superq(fname);
      %put %superq(msg);
      %return;
   %end;
   %put NOTE: (&SYSMACRONAME) %superq(fname) deleted.;
   %let rc=%sysfunc(filename(fileref));
%mend;
