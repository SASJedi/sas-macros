%macro renameFile(dir,oldname, newname);
   %local command rc slash;
   %if %superq(dir)= %then %do;
      %let type=ERROR;
      %PUT ;
      %PUT &TYPE: &SYSMACRONAME ERROR: DIR parameter is required.;
      %put &TYPE- &=dir &=oldname &=newname;
      %goto syntax;
   %end;
   %let type=NOTE;
   %if %qupcase(%superq(dir))=!HELP %then 
   %do;
      %let type=NOTE;
   %syntax:
      %PUT ;
      %PUT &TYPE: *&SYSMACRONAME Documentation *******************************;
      %PUT &TYPE-;
      %PUT &TYPE-  Renames files in a directory.;
      %PUT &TYPE-;
      %PUT &TYPE-  SYNTAX: %NRSTR(%%)%superq(sysmacroname)(dir,regex<,ext,sub>%));
      %PUT &TYPE-     dir     = fully qualified directory path;
      %PUT &TYPE-     oldname = current name of file;
      %PUT &TYPE-     newname = file new name;
      %PUT ;
      %PUT &TYPE-  Example: ;
      %PUT &TYPE-  %NRSTR(%%)%superq(sysmacroname)(c:\temp,myfile.txt,yourfile.txt);
      %PUT ;
      %PUT &TYPE- *************************************************************;
      %PUT ;
      %RETURN;
   %end;
   %if %superq(oldname)= %then %do;
      %let type=ERROR;
      %PUT ;
      %PUT &TYPE: &SYSMACRONAME ERROR: OLDNAME parameter is required.;
      %put &TYPE- &=dir &=oldname &=newname;
      %goto syntax;
   %end;
   %if %superq(newname)= %then %do;
      %let type=ERROR;
      %PUT ;
      %PUT &TYPE: &SYSMACRONAME ERROR: NEWNAME parameter is required.;
      %put &TYPE- &=dir &=oldname &=newname;
      %goto syntax;
   %end;
   %if %superq(sysscp)=WIN %then %let slash=\;
   %else %let slash=/;
   %if not %fileexist(%superq(dir)%superq(slash)%superq(oldname)) %then %do;
      %put ERROR: &SYSMACRONAME reports file %superq(dir)%superq(slash)%superq(oldname) does not exist.;
      %return;
   %end;
   %let command=rename "%superq(dir)%superq(slash)%superq(oldname)" "%superq(newname)";
   options noxwait;
   %sysexec(%superq(command));  
   options xwait;
   %if &SYSRC ne 0 %then %do;
      %put ERROR: &SYSMACRONAME reports command=%superq(command) produced return code &SYSRC;
      %put ERROR- %superq(oldname) probably did not get renamed properly.;
      %put;
   %end;
%mend renameFile;
