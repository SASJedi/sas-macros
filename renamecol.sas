%macro renameCol(dsn,rename);
   %local lib i msgtype;
   %let msgtype=NOTE;
   %if %superq(dsn)= %then %do;
      %let msgtype=ERROR;
      %put &msgtype: You must specify a data set name;
      %put &msgtype-;
   %syntax:
      %put &msgtype: &SYSMACRONAME macro help document:;
      %put &msgtype- Purpose: Renames a column or columns in a dataset.;
      %put &msgtype-;
      %put &msgtype- Syntax: %nrstr(%%)&SYSMACRONAME(dsn,rename);
      %put &msgtype- dsn:    Name of the dataset to modified.;
      %put &msgtype- rename: Renaming specification in oldname|newname format.;
      %put &msgtype-;
      %put &msgtype- Example: Rename columns Age and Sex in work.class:;
      %put &msgtype-  %nrstr(%%)&SYSMACRONAME(work.class,age|StudentAge Sex|Gender);
      %put &msgtype-;
      %put NOTE:   &SYSMACRONAME cannot be used in-line - it generates SAS code.;
      %put NOTE-   Use ? to print these notes.;
      %put &msgtype-;
      %return;
   %end; 
   %if %qsubstr(%SUPERQ(dsn),1,1)=! or %superq(dsn)=? %then %goto syntax;
   %if %sysfunc(exist(%superq(dsn)))=0 %then %do;
      %let msgtype=ERROR;
      %put &msgtype: &SYSMACRONAME: You must specify a valid dataset name;
      %put &msgtype- Dataset &dsn does not exist.;
      %put;
      %goto Syntax;
   %end;
   %let lib=%qscan(%superq(dsn),1,.);
   %if %superq(dsn)=%superq(lib) %then %let lib=WORK;
   %let dsn=%qscan(%superq(dsn),-1,.);
   %let dsn=%qupcase(%superq(dsn));
   %let lib=%qupcase(%superq(lib));
   %let rename=%qsysfunc(translate(%superq(rename),=,|));
proc datasets library=&lib nolist nodetails;
   modify &dsn ;
      rename &rename;
quit;
%mend;
