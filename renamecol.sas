%macro renameCol(dsn,rename,label,format);
   %local lib i msgtype;
   %let msgtype=NOTE;
   %if %superq(dsn)= %then %do;
      %let msgtype=ERROR;
      %put &msgtype: You must specify a data set name;
      %put &msgtype-;
   %syntax:
      %put &msgtype: &SYSMACRONAME macro help document:;
      %put &msgtype- Purpose: Renames a column or columns in a dataset.;
      %put &msgtype-;
      %put &msgtype- Syntax: %nrstr(%%)&SYSMACRONAME(dsn<,rename,label,format>);
      %put &msgtype- dsn:    Name of the dataset to modified. (required);
      %put &msgtype- You must specify at least one of these optional parameters:;
      %put &msgtype-    rename: Renaming specifications: oldname|newname;
      %put &msgtype-    label:  Label specifications: columnname|label;
      %put &msgtype-    format: Formatting specifications: columnname(s) format.;
      %put &msgtype-;
       %let MSGTYPE=NOTE;
      %put &msgtype- When renaming columns, use the new name for label and format specification.;
      %put &msgtype-;
      %put &msgtype- Examples: ;
      %put &msgtype- Rename two columns;
      %put &msgtype-    %nrstr(%%)&SYSMACRONAME(work.class,age|StudentAge Sex|Gender);
      %put &msgtype- Relabel two columns;
      %put &msgtype-    %nrstr(%%)&SYSMACRONAME(work.class,,Sex|Gender Age|AgeOfStudent);
      %put &msgtype- Reformat two columns;
      %put &msgtype-    %nrstr(%%)&SYSMACRONAME(work.class,,,height weight z5.1);
      %put &msgtype-;
      %put NOTE:   &SYSMACRONAME cannot be used in-line - it generates SAS code.;
      %put NOTE-   Use ? to print these notes.;
      %put &msgtype-;
      %return;
   %end; 
   %if %SUPERQ(dsn)= %then %do;;
      %let msgtype=ERROR;
      %put &msgtype: &SYSMACRONAME: You must specify a dataset name;
      %put;
      %goto Syntax;
   %end;
   %if %qsubstr(%SUPERQ(dsn),1,1)=! or %superq(dsn)=? %then %goto syntax;
   %if %sysfunc(exist(%superq(dsn)))=0 %then %do;
      %let msgtype=ERROR;
      %put &msgtype: &SYSMACRONAME: You must specify a valid dataset name;
      %put &msgtype- Dataset &dsn does not exist.;
      %put;
      %goto Syntax;
   %end;
   %if %superq(rename)= and %superq(label)= and %superq(format)= %then %do;
      %let msgtype=ERROR;
      %put &msgtype: &SYSMACRONAME: You must specify a rename, label or format parameter;
      %put &msgtype: You left rename, label and format values blank.;
      %put;
      %goto Syntax;
   %end;
   %let lib=%qscan(%superq(dsn),1,.);
   %if %superq(dsn)=%superq(lib) %then %let lib=WORK;
   %let dsn=%qscan(%superq(dsn),-1,.);
   %let dsn=%qupcase(%superq(dsn));
   %let lib=%qupcase(%superq(lib));

proc datasets library=&lib nolist nodetails;
   modify &dsn ;
   %if %superq(rename) ne %then %do;
      rename %qsysfunc(translate(%superq(rename),=,|));
   %end;
   %if %superq(label) ne %then %do;
      label %qsysfunc(translate(%superq(label),=,|));
   %end;
   %if %superq(format) ne %then %do;
      format %superq(FORMAT);
   %end;
quit;
%mend;
